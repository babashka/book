== Uberscript

The `--uberscript` option collects the expressions in
`BABASHKA_PRELOADS`, the command line expression or file, the main
entrypoint and all required namespaces from the classpath into a single
file. This can be convenient for debugging and deployment.

Here is an example that uses a function from the
https://github.com/clj-commons/fs[clj-commons/fs] library.

Let's first set the classpath:

[source,clojure]
----
$ export BABASHKA_CLASSPATH=$(clojure -Spath -Sdeps '{:deps {clj-commons/fs {:mvn/version "1.5.2"}}}')
----

Write a little script, say `glob.clj`:

[source,clojure]
----
(ns foo (:require [me.raynes.fs :as fs]))
(run! (comp println str)
      (fs/glob (first *command-line-args*)))
----

Now we can execute the script which uses the library:

[source,bash]
----
$ time bb glob.clj '*.md'
/Users/borkdude/Dropbox/dev/clojure/carve/README.md
bb glob.clj '*.md'   0.03s  user 0.02s system 70% cpu 0.064 total
----

Producing an uberscript with all required code:

[source,bash]
----
$ bb -f glob.clj --uberscript glob-uberscript.clj
----

To prove that we don't need the classpath anymore:

[source,bash]
----
$ unset BABASHKA_CLASSPATH
$ time bb glob-uberscript.clj '*.md'
/Users/borkdude/Dropbox/dev/clojure/carve/README.md
bb glob-uberscript.clj '*.md'   0.03s  user 0.02s system 93% cpu 0.049 total
----

Caveats:

* _Dynamic requires_. Building uberscripts works by running top-level
`ns` and `require` forms. The rest of the code is not evaluated. Code
that relies on dynamic requires may not work in an uberscript.
* _Resources_. The usage of `io/resource` assumes a classpath, so when
this is used in your uberscript, you still have to set a classpath and
bring the resources along.

If any of the above is problematic for your project, using an
link:#uberjar[uberjar] is a good alternative.

=== Carve

Uberscripts can be optimized by cutting out unused vars with
https://github.com/borkdude/carve[carve].

[source,bash]
----
$ wc -l glob-uberscript.clj
     607 glob-uberscript.clj
$ clojure -M:carve --opts '{:paths ["glob-uberscript.clj"] :aggressive true :silent true}'
$ wc -l glob-uberscript.clj
     172 glob-uberscript.clj
----

Note that the uberscript became 72% shorter. This has a beneficial
effect on execution time:

[source,bash]
----
$ time bb glob-uberscript.clj '*.md'
/Users/borkdude/Dropbox/dev/clojure/carve/README.md
bb glob-uberscript.clj '*.md'   0.02s  user 0.01s system 93% cpu 0.032 total
----

== Uberjar

Babashka can create uberjars from a given classpath and optionally a
main method:

[source,clojure]
----
$ cat src/foo.clj
(ns foo (:gen-class)) (defn -main [& args] (prn :hello))
$ bb -cp $(clojure -Spath) -m foo --uberjar foo.jar
$ bb foo.jar
:hello
----

When producing a classpath using the `clojure` or `deps.clj` tool,
Clojure itself, spec and the core specs will be on the classpath and
will therefore be included in your uberjar, which makes it bigger than
necessary:

[source,shell]
----
$ ls -lh foo.jar
-rw-r--r--  1 borkdude  staff   4.5M Aug 19 17:04 foo.jar
----

To exclude these dependencies, you can use the following
`:classpath-overrides` in your `deps.edn`:

[source,clojure]
----
{:aliases {:remove-clojure {:classpath-overrides {org.clojure/clojure nil
                                                  org.clojure/spec.alpha nil
                                                  org.clojure/core.specs.alpha nil}}}}
----

[source,clojure]
----
$ rm foo.jar
$ bb -cp $(clojure -A:remove-clojure -Spath) -m foo --uberjar foo.jar
$ bb foo.jar
:hello
$ ls -lh foo.jar
-rw-r--r--  1 borkdude  staff   871B Aug 19 17:07 foo.jar
----

If you want your uberjar to be compatible with the JVM, you'll need to
compile the main namespace. Babashka does not do compilation, so we use
Clojure on the JVM for that part:

[source,clojure]
----
$ rm foo.jar
$ mkdir classes
$ clojure -e "(require 'foo) (compile 'foo)"
foo
$ bb -cp $(clojure -Spath):classes -m foo --uberjar foo.jar
$ bb foo.jar
:hello
$ java -jar foo.jar
:hello
----

== System properties

Babashka sets the following system properties:

* `babashka.version`: the version string, e.g. `"1.2.0"`
* `babashka.main`: the `--main` argument
* `babashka.file`: the `--file` argument (normalized using
`.getAbsolutePath`)

[[__name__--__main__-pattern]]
== `__name__ == "__main__"` pattern

In Python scripts there is a well-known pattern to check if the current
file was the file invoked from the command line, or loaded from another
file: the `__name__ == "__main__"` pattern. In babashka this pattern can
be implemented with:

[source,clojure]
----
(= *file* (System/getProperty "babashka.file")
----

== Data readers

Data readers can be enabled by setting `*data-readers*` to a hashmap of
symbols to functions or vars:

[source,clojure]
----
$ bb "(set! *data-readers* {'t/tag inc}) #t/tag 1"
2
----

To preserve good startup time, babashka does not scan the classpath for
`data_readers.clj` files.

== Parsing command line arguments

Babashka ships with `clojure.tools.cli`:

[source,clojure]
----
(require '[clojure.tools.cli :refer [parse-opts]])

(def cli-options
  ;; An option with a required argument
  [["-p" "--port PORT" "Port number"
    :default 80
    :parse-fn #(Integer/parseInt %)
    :validate [#(< 0 % 0x10000) "Must be a number between 0 and 65536"]]
   ["-h" "--help"]])

(:options (parse-opts *command-line-args* cli-options))
----

[source,bash]
----
$ bb script.clj
{:port 80}
$ bb script.clj -h
{:port 80, :help true}
----

== Reader conditionals

Babashka supports reader conditionals by taking either the `:bb` or
`:clj` branch, whichever comes first. NOTE: the `:clj` branch behavior
was added in version 0.0.71, before that version the `:clj` branch was
ignored.

[source,clojure]
----
$ bb "#?(:bb :hello :clj :bye)"
:hello

$ bb "#?(:clj :bye :bb :hello)"
:bye

$ bb "[1 2 #?@(:bb [] :clj [1])]"
[1 2]
----

== Running tests

Babashka bundles `clojure.test`. To make CI scripts fail you can use a
simple runner like this:

[source,shell]
----
#!/usr/bin/env bash
bb -cp "src:test:resources" \
   -e "(require '[clojure.test :as t] '[borkdude.deps-test])
       (let [{:keys [:fail :error]} (t/run-tests 'borkdude.deps-test)]
         (System/exit (+ fail error)))"
----

== Spawning and killing a process

Use the `java.lang.ProcessBuilder` class.

Example:

[source,clojure]
----
user=> (def ws (-> (ProcessBuilder. ["python" "-m" "SimpleHTTPServer" "1777"]) (.start)))
#'user/ws
user=> (wait/wait-for-port "localhost" 1777)
{:host "localhost", :port 1777, :took 2}
user=> (.destroy ws)
nil
----

Also see this link:examples/process_builder.clj[example].

[[coreasync]]
== Core.async

In addition to `future`, `pmap`, `promise` and friends, you may use the
`clojure.core.async` namespace for asynchronous scripting. The following
example shows how to get first available value from two different
processes:

[source,clojure]
----
bb '
(defn async-command [& args]
  (async/thread (apply shell/sh "bash" "-c" args)))

(-> (async/alts!! [(async-command "sleep 2 && echo process 1")
                   (async-command "sleep 1 && echo process 2")])
    first :out str/trim println)'
process 2
----

Caveat: currently the `go` macro is available for compatibility with JVM
programs, but the implementation maps to `clojure.core.async/thread` and
the single exclamation mark operations (`<!`, `>!`, etc.) map to the
double exclamation mark operations (`<!!`, `>!!`, etc.). It will not
"park" threads, like on the JVM.

Examples like the following may still work, but will take a lot more
system resources than on the JVM and will break down for some high value
of `n`:

[source,clojure]
----
(require '[clojure.core.async :as async])

(def n 1000)

(let [cs (repeatedly n async/chan)
      begin (System/currentTimeMillis)]
  (doseq [c cs] (async/go (async/>! c "hi")))
  (dotimes [_ n]
    (let [[v _] (async/alts!! cs)]
      (assert (= "hi" v))))
  (println "Read" n "msgs in" (- (System/currentTimeMillis) begin) "ms"))
----

== HTTP

For making HTTP requests you can use:

* https://github.com/borkdude/babashka.curl[babashka.curl]. This library
is included with babashka and aliased as `curl` in the user namespace.
The interface is similar to that of
https://github.com/dakrone/clj-http[clj-http] but it will shell out to
`curl` to make requests.
* https://github.com/http-kit/http-kit[org.httpkit.client]
* `slurp` for simple `GET` requests
* https://github.com/babashka/clj-http-lite[clj-http-lite] as a library.
* `clojure.java.shell` or `babashka.process` for shelling out to your
favorite command line http client

=== Choosing the right client

If memory usage is a concern and you are downloading big files, choose
`babashka.curl` with `:as :stream` over `org.httpkit.client` since
http-kit holds the entire response in memory at once. Let's download a
200mb file with 10mb heap size:

[source,clojure]
----
$ bb -Xmx10m -e '(io/copy (:body (curl/get "http://ipv4.download.thinkbroadband.com/200MB.zip" {:as :stream})) (io/file "/tmp/200mb.zip"))'
----

With `babashka.curl` this works fine. However with `org.httpkit.client`
that won't work. Not even 190mb of heap will do:

[source,clojure]
----
$ bb -Xmx190m -e '(io/copy (:body @(org.httpkit.client/get "http://ipv4.download.thinkbroadband.com/200MB.zip" {:as :stream})) (io/file "/tmp/200mb.zip"))'
Sun Nov 08 23:01:46 CET 2020 [client-loop] ERROR - select exception, should not happen
java.lang.OutOfMemoryError: Array allocation too large.
----

If your script creates many requests with relatively small payloads,
choose `org.httpkit.client` over `babashka.curl` since `babashka.curl`
creates a `curl` process for each request.

In the future babashka (1.0.0?) may come with an HTTP client based on
the JVM 11 `java.net.http` package that ticks all the boxes (async,
HTTP/2, websockets, multi-part file uploads, sane memory usage) and is a
suitable replacement for all of the above options. If you know about a
GraalVM-friendly feature-complete well-maintained library, please reach
out!

=== HTTP over Unix sockets

This can be useful for talking to Docker:

[source,clojure]
----
(require '[clojure.java.shell :refer [sh]])
(require '[cheshire.core :as json])
(-> (sh "curl" "--silent"
        "--no-buffer" "--unix-socket"
        "/var/run/docker.sock"
        "http://localhost/images/json")
    :out
    (json/parse-string true)
    first
    :RepoTags) ;;=> ["borkdude/babashka:latest"]
----

== Shutdown hook

Adding a shutdown hook allows you to execute some code before the script
exits.

[source,clojure]
----
$ bb -e '(-> (Runtime/getRuntime) (.addShutdownHook (Thread. #(println "bye"))))'
bye
----

This also works when the script is interrupted with ctrl-c.

== JDBC

Babashka supports the
https://github.com/seancorfield/next-jdbc[`next.jdbc`] library along
with drivers for https://www.postgresql.org/[PostgresQL] and
http://hsqldb.org/[HSQLDB]. These features are not part of the standard
`bb` distribution but available via feature flags. See doc/build.md for
details on how to build babashka with these features. See this
link:test-resources/babashka/postgres_test.clj[test] for an example how
to use this.

Interacting with `psql`, `mysql` and the `sqlite` CLIs can be achieved
by shelling out. See the examples directory.

== Communicating with an nREPL server

Babashka comes with the https://github.com/nrepl/bencode[nrepl/bencode]
library which allows you to read and write bencode messages to a socket.
A simple example which evaluates a Clojure expression on an nREPL server
started with `lein repl`:

[source,clojure]
----
(ns nrepl-client
  (:require [bencode.core :as b]))

(defn nrepl-eval [port expr]
  (let [s (java.net.Socket. "localhost" port)
        out (.getOutputStream s)
        in (java.io.PushbackInputStream. (.getInputStream s))
        _ (b/write-bencode out {"op" "eval" "code" expr})
        bytes (get (b/read-bencode in) "value")]
    (String. bytes)))

(nrepl-eval 52054 "(+ 1 2 3)") ;;=> "6"
----

== Printing returned values

Babashka doesn't print a returned `nil` as lots of scripts end in
something side-effecting.

[source,shell]
----
$ bb '(:a {:a 5})'
5
$ bb '(:b {:a 5})'
$
----

If you really want to print the nil, you can use `(prn ..)` instead.

== Differences with Clojure

Babashka is implemented using the https://github.com/borkdude/sci[Small
Clojure Interpreter]. This means that a snippet or script is not
compiled to JVM bytecode, but executed form by form by a runtime which
implements a substantial subset of Clojure. Babashka is compiled to a
native binary using https://github.com/oracle/graal[GraalVM]. It comes
with a selection of built-in namespaces and functions from Clojure and
other useful libraries. The data types (numbers, strings, persistent
collections) are the same. Multi-threading is supported (`pmap`,
`future`).

Differences with Clojure:

* A pre-selected set of Java classes are supported. You cannot add Java
classes at runtime.
* Interpretation comes with overhead. Therefore loops are slower than in
Clojure on the JVM. In general interpretation yields slower programs
than compiled programs.
* No `deftype`, `definterface` and unboxed math.
* `defprotocol` and `defrecord` are implemented using multimethods and
regular maps. Ostensibly they work the same, but under the hood there
are no Java classes that correspond to them.
* Currently `reify` works only for one class at a time
* The `clojure.core.async/go` macro is not (yet) supported. For
compatibility it currently maps to `clojure.core.async/thread`. More
info link:#coreasync[here].

== link:doc/projects.md[Libraries, pods and projects]

A list of projects (scripts, libraries, pods and tools) known to work
with babashka.

== Pods

Pods are programs that can be used as a Clojure library by babashka.
Documentation is available in the
https://github.com/babashka/babashka.pods[library repo].

== Package babashka script as a AWS Lambda

AWS Lambda runtime doesn't support signals, therefore babashka has to
disable handling of SIGINT and SIGPIPE. This can be done by setting
`BABASHKA_DISABLE_SIGNAL_HANDLERS` to `true`.

== Articles, podcasts and videos

* https://youtu.be/RogyxI-GaGQ[Writing Clojure on the Command Line with
Babashka], a talk by Nate Jones.
* http://www.karimarttila.fi/clojure/2020/09/01/using-clojure-in-command-line-with-babashka.html[Using
Clojure in Command Line with Babashka], a blog article by Kari Marttila.
* https://youtu.be/3EUMA6bd-xQ[Babashka and GraalVM; taking Clojure to
new places], a talk by Michiel Borkent at
https://www.meetup.com/Clojure-NYC/[Clojure/NYC].
* https://blog.davemartin.me/posts/import-a-csv-into-kafka-using-babashka/[Import
a CSV into Kafka, using Babashka] by Dave Martin
* https://amontalenti.com/2020/07/11/babashka[Learning about babashka],
a blog article by Andrew Montalenti
* https://www.youtube.com/watch?v=3Q4GUiUIrzg&feature=emb_logo[Babashka
Pods] presentation by Michiel Borkent at the
http://meetup.com/The-Dutch-Clojure-Meetup[Dutch Clojure Meetup].
* https://tech.toyokumo.co.jp/entry/aws_logs_babashka[AWS Logs using
Babashka], a blog published by https://toyokumo.co.jp/[Toyokumo].
* https://www.therepl.net/episodes/36/[The REPL podcast] Michiel Borkent
talks about https://github.com/borkdude/clj-kondo[clj-kondo],
https://github.com/borkdude/jet[Jet], Babashka, and
https://github.com/oracle/graal[GraalVM] with Daniel Compton.
* https://youtu.be/0YmZYnwyHHc[Implementing an nREPL server for
babashka]: impromptu presentation by Michiel Borkent at the online
http://meetup.com/The-Dutch-Clojure-Meetup[Dutch Clojure Meetup]
* https://soundcloud.com/user-959992602/s3-e5-babashka-with-michiel-borkent[ClojureScript
podcast] with Jacek Schae interviewing Michiel Borkent
* https://www.youtube.com/watch?v=Nw8aN-nrdEk[Babashka talk at ClojureD]
(https://speakerdeck.com/borkdude/babashka-and-the-small-clojure-interpreter-at-clojured-2020[slides])
by Michiel Borkent
* https://juxt.pro/blog/posts/babashka.html[Babashka: a quick example]
by Malcolm Sparks
* https://stuartsierra.com/2019/12/21/clojure-start-time-in-2019[Clojure
Start Time in 2019] by Stuart Sierra
* https://lambdaisland.com/blog/2019-12-19-advent-of-parens-19-advent-of-random-hacks[Advent
of Random Hacks] by Arne Brasseur
* https://lambdaisland.com/blog/2019-12-05-advent-of-parens-5-clojure-in-the-shell[Clojure
in the Shell] by Arne Brasseur
* https://purelyfunctional.tv/issues/purelyfunctional-tv-newsletter-351-clojure-tool-babashka/[Clojure
Tool] by Eric Normand

== link:doc/build.md[Building babashka]

== link:doc/dev.md[Developing Babashka]

== Including new libraries or classes

Before new libraries or classes go into the standardly distributed
babashka binary, these evaluation criteria are considered:

* The library or class is useful for general purpose scripting.
* Adding the library or class would make babashka more compatible with
Clojure libraries relevant to scripting.
* The library cannot be interpreted by with babashka using
`--classpath`.
* The functionality can't be met by shelling out to another CLI or can't
be written as a small layer over an existing CLI (like `babashka.curl`)
instead.
* The library cannot be implemented a
https://github.com/babashka/babashka.pods[pod].

If not all of the criteria are met, but adding a feature is still useful
to a particular company or niche, adding it behind a feature flag is
still a possibility. This is currently the case for `next.jdbc` and the
`PostgresQL` and `HSQLDB` database drivers. Companies interested in
these features can compile an instance of babashka for their internal
use. Companies are also free to make forks of babashka and include their
own internal libraries. If their customized babashka is interesting to
share with the world, they are free to distribute it using a different
binary name (like `bb-sql`, `bb-docker`, `bb-yourcompany`, etc.). See
the link:doc/build.md#feature-flags[feature flag documentation] and the
implementation of the existing feature flags
(https://github.com/borkdude/babashka/commit/02c7c51ad4b2b1ab9aa95c26a74448b138fe6659[example
commit]).

== Babashka book

In the future we will migrate examples from this README to the babashka
https://book.babashka.org/[book]. This is work in progress and you are
welcome to contribute.

== Related projects

* https://planck-repl.org/[planck]
* https://github.com/candid82/joker[joker]
* https://github.com/dundalek/closh[closh]
* https://github.com/anmonteiro/lumo[lumo]

== Examples

link:examples/README.md[A collection of example scripts].

== Thanks

* https://www.adgoji.com/[adgoji] for financial support
* https://circleci.com/[CircleCI] for CI and additional support
* https://github.com/tonsky[Nikita Prokopov] for the logo
* https://github.com/borkdude/babashka/graphs/contributors[contributors]
and other users posting issues with bug reports and ideas

== Contributors

=== Code Contributors

This project exists thanks to all the people who contribute.
[link:doc/dev.md[Contribute]].

=== Financial Contributors

Become a financial contributor and help us sustain our community.
[https://opencollective.com/babashka/contribute[Contribute]]

==== Individuals

==== Organizations

Support this project with your organization. Your logo will show up here
with a link to your website.
[https://opencollective.com/babashka/contribute[Contribute]]

== License

Copyright Â© 2019-2020 Michiel Borkent

Distributed under the EPL License. See LICENSE.

This project contains code from:

* Clojure, which is licensed under the same EPL License.
