[[tasks]]
== Task runner

=== Introduction

People often use a `Makefile`, `Justfile`, `npm scripts` or `lein` aliases in
their (clojure) projects to remember complex invocations and to create shortcuts
for them. Since version 0.4.0, babashka supports a similar feature as part of
the `bb.edn` project configuration file. For a general overview of what's
available in `bb.edn`, go to <<project-setup>>.

The tasks configuration lives under the `:tasks` key and can be used together
with `:paths` and `:deps`:

[source,clojure]
----
{:paths ["script"]
 :deps {medley/medley {:mvn/version "1.3.0"}}
 :min-bb-version "0.4.0"
 :tasks
 {clean (shell "rm -rf target")
 ...}
 }
----

In the above example we see a simple task called `clean` which invokes the
`shell` command, to remove the `target` directory. You can invoke this task from
the command line with:

[source,bash]
----
$ bb run clean
----

Babashka also accepts a task name without explicitly mentioning `run`:

[source,bash]
----
$ bb clean
----

To make your tasks more cross-platform friendly, you can use the built-in
https://github.com/babashka/fs[babashka.fs] library. To use libraries in tasks,
use the `:requires` option:

[source,clojure]
----
{:tasks
 {:requires ([babashka.fs :as fs])
  clean (fs/delete-tree "target")
  }
 }
----

Tasks accept arbitrary Clojure expressions. E.g. you can print something when executing the task:

[source,clojure]
----
{:tasks
 {:requires ([babashka.fs :as fs])
  clean (do (println "Removing target folder.")
            (fs/delete-tree "target"))
  }
 }
----

[source,bash]
----
$ bb clean
Removing target folder.
----

=== Run

The babashka `run` subcommand accepts these additional options:

- `--parallel`: invoke task dependencies in parallel.
+
[source,clojure]
----
{:tasks
 {:init (def log (Object.))
  :enter (locking log
           (println (str (:name (current-task))
                         ":")
                    (java.util.Date.)))
  a (Thread/sleep 5000)
  b (Thread/sleep 5000)
  c {:depends [a b]}
  d {:task (time (run 'c))}}}
----
+
[source,bash]
----
$ bb run --parallel d
d: #inst "2021-05-08T14:14:56.322-00:00"
a: #inst "2021-05-08T14:14:56.357-00:00"
b: #inst "2021-05-08T14:14:56.360-00:00"
c: #inst "2021-05-08T14:15:01.366-00:00"
"Elapsed time: 5023.894512 msecs"
----
+
Also see <<parallel>>.
- `--prn`: print the result from the task expression:
+
[source,clojure]
----
{:tasks {sum (+ 1 2 3)}}
----
+
[source,bash]
----
$ bb run --prn sum
6
----
+
Unlike scripts, babashka tasks do not print their return value.

=== Hooks

The task runner exposes the following hooks:

==== :init

The `:init` is for expressions that are executed before any of the tasks are
executed. It is typically used for defining helper functions and constants:

[source,clojure]
----
{:tasks
 {:init (defn env [s] (System/getenv s))
  print-env (println (env (first *command-line-args*)))
  }
 }
----

[source,bash]
----
$ FOO=1 bb print-env FOO
1
----

==== :enter, :leave

The `:enter` hook is executed before each task. This is typically used to print
the name of a task, which can be obtained using the `current-task` function:

[source,clojure]
----
{:tasks
 {:init (defn env [s] (System/getenv s))
  :enter (println "Entering:" (:name (current-task)))
  print-env (println (env (first *command-line-args*)))
  }
 }
----

[source,bash]
----
$ FOO=1 bb print-env FOO
Entering: print-env
1
----

The `:leave` hook is similar to `:enter` but is executed after each task.

Both hooks can be overriden as task-local options. Setting them to `nil` will
disable them for specific tasks (see <<_task_local_options>>).

=== Command line arguments

The task runner is unopinionated when it comes to command line arguments.
Arguments are available in `*command-line-args*`. You are free
to parse them using the built-in `tools.cli` library, use
https://github.com/nubank/docopt.clj[docopt], convert them to EDN using `(map
edn/read-string *command-line-args*)` or do whatever you want to handle them.

You can re-bind `*command-line-args*` to ensure functions see a different set of
arguments:

[source,clojure]
----
{:tasks
 {:init (do (defn print-args []
              (prn (:name (current-task))
                   *command-line-args*)))
  bar (print-args)
  foo (do (print-args)
          (binding [*command-line-args* (next *command-line-args*)]
            (run 'bar)))}}
----

[source,bash]
----
$ bb foo 1 2 3
foo ("1" "2" "3")
bar ("2" "3")
----

=== Task-local options

Instead of naked expressions, tasks can be defined as maps with options. The
task expression should then be moved to the `:task` key:

[source,clojure]
----
{:tasks
 {
  clean {:doc "Removes target folder"
         :requires ([babashka.fs :as fs])
         :task (fs/delete-tree "target")}
  }
 }
----

Tasks support the `:doc` option which gives it a docstring which is printed
when invoking `bb tasks` on the command line. Other options include:

- `:requires`: task-specific namespace requires.
- `:extra-paths`: add paths to the classpath.
- `:extra-deps`: add extra dependencies to the classpath.
- `:enter`, `:leave`: override the global `:enter`/`:leave` hook.

=== Discoverability

When invoking `bb tasks`, babashka prints a list of all tasks found in `bb.edn` in the order of appearance. E.g. in the https://github.com/clj-kondo/clj-kondo.lsp[clj-kondo.lsp] project it prints:

[source,bash]
----
$ bb tasks
The following tasks are available:

recent-clj-kondo   Detects most recent clj-kondo version from clojars
update-project-clj Updates project.clj with most recent clj-kondo version
java1.8            Asserts that we are using java 1.8
build-server       Produces lsp server standalone jar
lsp-jar            Copies renamed jar for upload to clj-kondo repo
upload-jar         Uploads standalone lsp server jar to clj-kondo repo
vscode-server      Copied lsp server jar to vscode extension
vscode-version     Prepares package.json with up to date clj-kondo version
vscode-publish     Publishes vscode extension to marketplace
ovsx-publish       Publishes vscode extension to ovsx thing
publish            The mother of all tasks: publishes everything needed for new release
----

==== Terminal tab-completion

===== zsh

Add this to your `.zshrc` to get tab-complete feature on ZSH.

[source,bash]
----
_bb_tasks() {
    local matches=(`bb tasks |tail -n +3 |cut -f1 -d ' '`)
    compadd -a matches
    _files # autocomplete filenames as well
}
compdef _bb_tasks bb
----

===== bash

Add this to your `.bashrc` to get tab-complete feature on bash.

[source,bash]
----
_bb_tasks() {
    COMPREPLY+=(`bb tasks |tail -n +3 |cut -f1 -d ' '`)
}
# autocomplete filenames as well
complete -f -F _bb_tasks bb
----

===== fish

Add this to your `.config/fish/completions/bb.fish` to get tab-complete feature on Fish shell.

[source,bash]
----
function __bb_complete_tasks
  if not test "$__bb_tasks"
    set -g __bb_tasks (bb tasks |tail -n +3 |cut -f1 -d ' ')
  end

  printf "%s\n" $__bb_tasks
end

complete -c bb -a "(__bb_complete_tasks)" -d 'tasks'
----

=== Tasks API

The `babashka.tasks` namespace exposes the following functions: `run`, `shell`,
`clojure` and `current-task`. They are implicitly imported, thus available
without a namespace prefix.

==== run

Tasks provide the `run` function to explicitly invoke another task:

[source,clojure]
----
{:tasks
 {:requires ([babashka.fs :as fs])

  clean (do
          (println "Removing target folder.")
          (fs/delete-tree "target"))
  uberjar (do
            (println "Making uberjar")
            (clojure "-X:uberjar"))
  uberjar:clean (do (run 'clean)
                    (run 'uberjar))}
 }
----

When running `bb uberjar:clean`, first the `clean` task is executed and the `uberjar`:

[source,bash]
----
$ bb uberjar:clean
Removing target folder.
Making uberjar
----

The `clojure` function in the above example executes a clojure process using https://github.com/borkdude/deps.clj[deps.clj]. See <<tasks:clojure>> for more info

The `run` function accepts an additional map with options:

===== :parallel

The `:parallel` option executes dependencies of the invoked task in parallel
(when possible). See <<parallel>>.

==== shell

Both `shell` and `clojure` return a
https://github.com/babashka/babashka.process[process] object which returns the
`:exit` code among other info. By default these functions will throw an
exception when a non-zero exit code was returned and they will inherit the
stdin/stdout/stderr from the babashka process.

[source,clojure]
----
{:tasks
 {
  ls (shell "ls foo")
 }
}
----

[source,bash]
----
$ bb ls
ls: foo: No such file or directory
Error while executing task: ls
$ echo $?
1
----

You can opt out of this behavior by using the `:continue` option:

[source,clojure]
----
{:tasks
 {
  ls (shell {:continue true} "ls foo")
 }
}
----

[source,bash]
----
$ bb ls
ls: foo: No such file or directory
$ echo $?
0
----

When you want to redirect output to a file instead, you can provide the `:out` option.

[source,clojure]
----
(shell {:out "file.txt"} "echo hello")
----

To run a program in another directory, you can use the `:dir` option:

[source,clojure]
----
(shell {:dir "subproject"} "ls")
----

To set environment variables with `shell` or `clojure`:

[source,clojure]
----
(shell {:extra-env {"FOO" "BAR"}} "printenv FOO")
----

Other supported options are similar to those of
https://github.com/babashka/babashka.process[`babashka.process/process`].

The process is executed synchronously: i.e. babashka will wait for the process
to finish before executing the next expression. If this doesn't fit your use
case, you can use
https://github.com/babashka/babashka.process[`babashka.process/process`]
directly instead. These two invocations are roughly equivalent:

[source,clojure]
----
(require '[babashka.process :as p :refer [process]]
         '[babashka.tasks :as tasks])

(tasks/shell {:dir "subproject"} "npm install")

(-> (process "npm install" {:dir "subproject" :inherit true})
    (p/check))
----

[[tasks:clojure]]
==== clojure

The `clojure` function starts a Clojure process using
https://github.com/borkdude/deps.clj[deps.clj]. The interface is exactly the
same as the clojure CLI. E.g. to evaluate an expression:

[source,clojure]
----
{:tasks {eval (clojure "-M -e '(+ 1 2 3)'")}}
----

or to invoke clj-kondo as a library:

[source,clojure]
----
{:tasks {eval (clojure {:dir "subproject"} "-M:clj-kondo")}}
----

The `clojure` task function behaves similar to `shell` with respect to the exit
code, return value and supported options, except when it comes to features that
do not start a process, but only do some printing. E.g. you can capture the
classpath using:

```clojure
(with-out-str (clojure "-Spath"))
```

because this operation doesn't start a process but prints to `*out*`.

To run a `clojure` task in another directory:

[source,clojure]
----
{:tasks {eval (clojure {:dir "subproject"} "-M:clj-kondo")}}
----

[[current-task]]
==== current-task

The `current-task` function returns a map representing the currently running task. This function is typically used in the `:enter` and `:leave` hooks.

=== Dependencies between tasks

Dependencies between tasks can be declared using `:depends`:

[source,clojure]
----
{:tasks {:requires ([babashka.fs :as fs])
         -target-dir "target"
         -target {:depends [-target-dir]
                  :task (fs/create-dirs -target-dir)}
         -jar-file {:depends [-target]
                    :task "target/foo.jar"}

         jar {:depends [-target -jar-file]
              :task (when (seq (fs/modified-since -jar-file
                                             (fs/glob "src" "**.clj")))
                      (spit -jar-file "test")
                      (println "made jar!"))}
         uberjar {:depends [jar]
                  :task (println "creating uberjar!")}}}
----

The `fs/modified-since` function returns a seq of all newer files compared to a
target, which can be used to prevent rebuilding artifacts when not necessary.

Alternatively you can use the `:init` hook to define vars, require namespaces,
etc.:

[source,clojure]
----
{:tasks {:requires ([babashka.fs :as fs])
         :init (do (def target-dir  "target")
                   (def jar-file "target/foo.jar"))
         -target {:task (fs/create-dirs target-dir)}
         jar {:depends [-target]
              :task (when (seq (fs/modified-since jar-file
                                             (fs/glob "src" "**.clj")))
                      (spit jar-file "test")
                      (println "made jar!"))}
         uberjar {:depends [jar]
                  :task (println "creating uberjar!")}}}
----

It is common to define tasks that only serve as a helper to other tasks. To not
expose these tasks in the output of `bb tasks`, you can start their name with a
hyphen.

[[parallel]]
=== Parallel tasks

The `:parallel` option executes dependencies of the invoked task in parallel
(when possible). This can be used to speed up execution, but also to have
multiple tasks running in parallel for development:

[source,clojure]
----
dev         {:doc  "Runs app in dev mode. Compiles cljs, less and runs JVM app in parallel."
             :task (run '-dev {:parallel true})}       ;; <1>
-dev        {:depends [dev:cljs dev:less dev:backend]} ;; <2>
dev:cljs    {:doc  "Runs front-end compilation"
             :task (clojure "-M:frontend:cljs/dev")}
dev:less    {:doc  "Compiles less"
             :task (clojure "-M:frontend:less/dev")}
dev:backend {:doc  "Runs backend in dev mode"
             :task (clojure (str "-A:backend:backend/dev:" platform-alias)
                            "-X" "dre.standalone/start")}
----

<1> The `dev` task invokes the (private) `-dev` task in parallel
<2> The `-dev` task depends on three other tasks which are executed simultaneously.


=== Invoking a main function

Invoking a main function can be done by providing a fully qualified
symbol:

[source,clojure]
----
{:tasks
  {foo-bar foo.bar}}
----

or using a fully qualified symbol `foo.bar/baz` so you can accommodate
multiple main functions in one namespace.

The namespace `foo.bar` will be automatically required and the function
will be invoked with `*command-line-args*`:

[source,clojure]
----
$ bb foo-bar 1 2 3
----

=== REPL

To get a REPL within a task, you can use `clojure.main/repl`:

[source,clojure]
----
{:tasks {repl (clojure.main/repl)}}
----

Alternatively, you can use `babashka.tasks/run` to invoke a task from a REPL.

=== Real world examples

* https://github.com/borkdude/antq/blob/bb-run/bb.edn[antq]
* https://github.com/borkdude/mach/blob/bb-run/examples/app/bb.edn[mach]
* https://gist.github.com/borkdude/35bc0a20bd4c112dec2c5645f67250e3[bb.edn
at Doctor Evidence]
* https://github.com/clj-kondo/clj-kondo.lsp/blob/master/bb.edn[clj-kondo.lsp]
* https://github.com/wilkerlucio/pathom-viz/blob/master/bb.edn[pathom]
* https://github.com/redstarssystems/rssyslib/blob/develop/bb.edn[rssyslib]
* https://github.com/clj-commons/rewrite-clj/blob/main/bb.edn[rewrite-clj]
* https://gist.github.com/delyada/9f50fa7466358e55f27e4e6b4314242f
* https://github.com/rwstauner/jirazzz/blob/main/bb.edn[jirazzz]

=== Naming

==== Valid names

When running a task, babashka assembles a small program which defines vars
bound to the return values of tasks. This brings the limitation that you can
only choose names for your tasks that are valid as var names. You can't name
your task `foo/bar` for this reason. If you want to use delimiters to indicate
some sort of grouping, you can do it like `foo-bar`, `foo:bar` or `foo_bar`.

Names starting with a `-` are considered "private" and not listed in the
`bb tasks` output.

==== Conflicting file / task / subcommand names

`bb <option>` is resolved in the order of file > task > subcommand.

Escape hatches in case of conflicts:

- execute relative file as `bb ./foo`
- execute task as `bb run foo`
- execute subcommand as `bb --foo`

==== Conflicting task and clojure.core var names

You can name a task similar to a core var, let's say: `format`. If you want to
refer to the core var, it is recommended to use the fully qualified
`clojure.core/format` in that case, to avoid conflicts in `:enter` and `:leave`
expressions and when using the `format` task as a dependency.

=== Syntax

Because `bb.edn` is an EDN file, you cannot use all of Clojure's syntax in
expressions. Most notably:

- You cannot use `#(foo %)`, but you can use `(fn [x] (foo x))`
- You cannot use `@(foo)` but you can use `(deref foo)`
- You cannot use `#"re"` but you can use `(re-pattern "re")`
- Single quotes are accidentally supported in some places, but are better
  avoided: `{:task '(foo)}` does not work, but `{:task (quote (foo))` does
  work. When requiring namespaces, use the `:requires` feature in favor of doing
  it manually using `(require '[foo])`.
