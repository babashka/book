[[recipes]]
== Recipes

=== Running tests

Babashka bundles `clojure.test`. To run tests you can write a test runner script. Given the following project structure:

[source,shell]
----
.
├── src
│   └──...
└── test
    └── your
        ├── test_a.clj
        └── test_b.clj
----

.test-runner.clj
[source,clojure]
----
#!/usr/bin/env bb

(require '[clojure.test :as t]
         '[babashka.classpath :as cp])

(cp/add-classpath "src:test")                        ;; <1>

(require 'your.test-a 'your.test-b)                  ;; <2>

(def test-results
  (t/run-tests 'your.test-a 'your.test-b))           ;; <3>

(def failures-and-errors
  (let [{:keys [:fail :error]} test-results]
    (+ fail error)))                                 ;; <4>

(System/exit failures-and-errors)                    ;; <5>
----

<1> Add sources and tests to the classpath
<2> Require the test namespaces
<3> Run all tests in the test namespaces
<4> Extract failures and errors
<5> Exit the test script with a non-zero exit code when there are failures or errors

[[main_file]]
=== Main file

In Python scripts there is a well-known pattern to check if the current
file was the file invoked from the command line, or loaded from another
file: the `__name__ == "__main__"` pattern. In babashka this pattern can
be implemented with:

[source,clojure]
----
(= *file* (System/getProperty "babashka.file"))
----

Combining this with a conditional invocation of `-main` creates a script file that is safe to load at a REPL, and easy to invoke at the CLI.

[source,clojure]
---
#!/usr/bin/env bb

;; Various functions defined here

(defn -main [& args]
;; Implementation of main
)

(when (= *file* (System/getProperty "babashka.file"))
  (apply -main *command-line-args*))
---

This can be exceedingly handy for editing complex scripts interactively, while not being able to adjust how they are invoked by other tools.

=== Shutdown hook

Adding a shutdown hook allows you to execute some code before the script
exits.

[source,clojure]
----
$ bb -e '(-> (Runtime/getRuntime) (.addShutdownHook (Thread. #(println "bye"))))'
bye
----

This also works when the script is interrupted with ctrl-c.

=== Printing returned values

Babashka doesn't print a returned `nil` as lots of scripts end in
something side-effecting.

[source,shell]
----
$ bb -e '(:a {:a 5})'
5
$ bb -e '(:b {:a 5})'
$
----

If you really want to print the nil, you can use `(prn ..)` instead.

=== HTTP requests

For making HTTP requests you can use:

* https://github.com/borkdude/babashka.curl[babashka.curl]. This library
is included with babashka and aliased as `curl` in the user namespace.
The interface is similar to that of
https://github.com/dakrone/clj-http[clj-http] but it will shell out to
`curl` to make requests.
* https://github.com/http-kit/http-kit[org.httpkit.client]
* `slurp` for simple `GET` requests
* https://github.com/babashka/clj-http-lite[clj-http-lite] as a library.
* `clojure.java.shell` or `babashka.process` for shelling out to your
favorite command line http client

==== Choosing the right client

If memory usage is a concern and you are downloading big files, choose
`babashka.curl` with `:as :stream` over `org.httpkit.client` since
http-kit holds the entire response in memory at once. Let's download a
200mb file with 10mb heap size:

[source,clojure]
----
$ bb -Xmx10m -e '(io/copy (:body (curl/get "http://ipv4.download.thinkbroadband.com/200MB.zip" {:as :stream})) (io/file "/tmp/200mb.zip"))'
----

With `babashka.curl` this works fine. However with `org.httpkit.client`
that won't work. Not even 190mb of heap will do:

[source,clojure]
----
$ bb -Xmx190m -e '(io/copy (:body @(org.httpkit.client/get "http://ipv4.download.thinkbroadband.com/200MB.zip" {:as :stream})) (io/file "/tmp/200mb.zip"))'
Sun Nov 08 23:01:46 CET 2020 [client-loop] ERROR - select exception, should not happen
java.lang.OutOfMemoryError: Array allocation too large.
----

If your script creates many requests with relatively small payloads,
choose `org.httpkit.client` over `babashka.curl` since `babashka.curl`
creates a `curl` process for each request.

In the future babashka (1.0.0?) may come with an HTTP client based on
the JVM 11 `java.net.http` package that ticks all the boxes (async,
HTTP/2, websockets, multi-part file uploads, sane memory usage) and is a
suitable replacement for all of the above options. If you know about a
GraalVM-friendly feature-complete well-maintained library, please reach
out!

==== HTTP over Unix sockets

This can be useful for talking to Docker:

[source,clojure]
----
(require '[clojure.java.shell :refer [sh]])
(require '[cheshire.core :as json])
(-> (sh "curl" "--silent"
        "--no-buffer" "--unix-socket"
        "/var/run/docker.sock"
        "http://localhost/images/json")
    :out
    (json/parse-string true)
    first
    :RepoTags) ;;=> ["borkdude/babashka:latest"]
----

[[core_async]]
=== Core.async

In addition to `future`, `pmap`, `promise` and friends, you may use the
`clojure.core.async` namespace for asynchronous scripting. The following
example shows how to get first available value from two different
processes:

[source,clojure]
----
bb -e '
(defn async-command [& args]
  (async/thread (apply shell/sh "bash" "-c" args)))

(-> (async/alts!! [(async-command "sleep 2 && echo process 1")
                   (async-command "sleep 1 && echo process 2")])
    first :out str/trim println)'
process 2
----

Caveat: currently the `go` macro is available for compatibility with JVM
programs, but the implementation maps to `clojure.core.async/thread` and
the single exclamation mark operations (`<!`, `>!`, etc.) map to the
double exclamation mark operations (`<!!`, `>!!`, etc.). It will not
"park" threads, like on the JVM.

Examples like the following may still work, but will take a lot more
system resources than on the JVM and will break down for some high value
of `n`:

[source,clojure]
----
(require '[clojure.core.async :as async])

(def n 1000)

(let [cs (repeatedly n async/chan)
      begin (System/currentTimeMillis)]
  (doseq [c cs] (async/go (async/>! c "hi")))
  (dotimes [_ n]
    (let [[v _] (async/alts!! cs)]
      (assert (= "hi" v))))
  (println "Read" n "msgs in" (- (System/currentTimeMillis) begin) "ms"))
----

=== Interacting with an nREPL server

Babashka comes with the https://github.com/nrepl/bencode[nrepl/bencode]
library which allows you to read and write bencode messages to a socket.
A simple example which evaluates a Clojure expression on an nREPL server
started with `lein repl`:

[source,clojure]
----
(ns nrepl-client
  (:require [bencode.core :as b]))

(defn nrepl-eval [port expr]
  (let [s (java.net.Socket. "localhost" port)
        out (.getOutputStream s)
        in (java.io.PushbackInputStream. (.getInputStream s))
        _ (b/write-bencode out {"op" "eval" "code" expr})
        bytes (get (b/read-bencode in) "value")]
    (String. bytes)))

(nrepl-eval 52054 "(+ 1 2 3)") ;;=> "6"
----


=== Running from Cygwin/Git Bash

On Windows, `bb` can be invoked from the bash shell directly:

[source,shell]
----
$ bb -e '(+ 1 2 3)'
6
----

However, creating a script that invokes `bb` via a shebang leads to an error if
the script is not in the current directory. Suppose you had the following script
named `hello` on your path:

[source,clojure]
----
#!/usr/bin/env bb
(println "Hello, world!")
----

[source,shell]
----
$ hello
----- Error --------------------------------------------------------------------
Type:     java.lang.Exception
Message:  File does not exist: /cygdrive/c/path/to/hello
----

The problem here is that the shell is passing a Cygwin-style path to `bb`, but
`bb` can't recognize it because it wasn't compiled with Cygwin.

The solution is to create a wrapper script that converts the Cygwin-style path
to a Windows-style path before invoking `bb`. Put the following into a script
called `bbwrap` somewhere on your Cygwin path, say in `/usr/local/bin/bbwrap`:

[source,shell]
----
#!/bin/bash
SCRIPT=$1
shift
bb.exe $(cygpath -w $SCRIPT) $@
----

Make sure to fix your original script to invoke `bbwrap` instead of `bb`
directly:

[source,clojure]
----
#!/usr/bin/env bbwrap
(println "Hello, world!")
----
