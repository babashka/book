[[project-setup]]
== Project setup

=== bb.edn

Since version 0.3.1, babashka supports a local `bb.edn` file to manage a project.

=== :paths and :deps

You can declare one or multiple paths and dependencies so they are automatically
added to the classpath:

[source,clojure]
----
{:paths ["bb"]
 :deps {medley/medley {:mvn/version "1.3.0"}}}
----

If we have a project that has a `deps.edn` and would like to reuse those deps in `bb.edn`:

[source, clojure]
----
{:deps {your-org/your-project {:local/root "."}}}
----

`bb.edn` applies to the local project, and dependencies defined in
this files are never shared with other projects. This is typically
what you want when writing a script or tool. By contrast, `deps.edn`
is useful when creating libraries that are used by other projects.

NOTE: Use a unique name to refer to your project's `deps.edn`, the same name that
you would otherwise use when referring to your project as a dependency.

If we have a main function in a file called `bb/my_project/main.clj` like:

----
(ns my-project.main
  (:require [medley.core :as m]))

(defn -main [& _args]
  (prn (m/index-by :id [{:id 1} {:id 2}])))
----

we can invoke it like:

[source,clojure]
----
$ bb -m my-project.main
{1 {:id 1}, 2 {:id 2}}
----

See <<main-function>> for more details on how to invoke a function from the command line.

The `:deps` entry is managed by https://github.com/borkdude/deps.clj[deps.clj]
and requires a `java` installation to resolve and download dependencies.

=== :min-bb-version

Since version 0.3.6, babashka supports the `:min-bb-version` where the minimal
babashka version can be declared:

[source,clojure]
----
{:paths ["src"]
 :deps {medley/medley {:mvn/version "1.3.0"}}
 :min-bb-version "0.3.7"}
----

When using an older bb version (that supports `:min-bb-version`), babashka will
print a warning:

[source]
----
WARNING: this project requires babashka 0.3.7 or newer, but you have: 0.3.6
----

=== :tasks

Since babashka 0.4.0 the `bb.edn` file supports the `:tasks` entry which
describes tasks that you can run in the current project. The tasks feature is
similar to what people use `Makefile`, `Justfile` or `npm run` for. See <<tasks>> for more details.

=== Script-adjacent bb.edn

Since babashka 1.3.177 a `bb.edn` file relative to the invoked file is
respected. This makes writing system-global scripts with dependencies easier.

Given a `bb.edn`:

[source,clojure]
----
{:deps {medley/medley {:mvn/version "1.3.0"}}}
----

and a script `medley.bb`:

[source,clojure]
----
#!/usr/bin/env bb

(ns medley
  (:require [medley.core :as medley]))

(prn (medley/index-by :id [{:id 1}]))
----

Assuming that `medley.bb` is executable (`chmod +x medley.bb`), you can directly execute it in the current directory:

[source,shell]
----
~/my_project $ ./medley.bb
{1 {:id 1}}
----

To execute this script from anywhere on the system, you just have to add it to the `PATH`:

[source,shell]
----
/tmp $ export PATH=$PATH:~/my_project # ensure script is on path
/tmp $ medley.bb # works, respects ~/my_project/bb.edn file with :deps
{1 {:id 1}}
----

Of course you can just call your script `medley` without the `.bb` extension.

[[script-adjacent-bb-edn-windows]]
==== Windows

On Windows bash shebangs are not supported. An alternative is to create a script-adjacent `.bat` file, e.g `medley.bat`:

[source,shell]
----
@echo off
set ARGS=%*
set SCRIPT=%~dp0medley.bb
bb %SCRIPT% %ARGS%
----

Then add this script to your `%PATH%`:

[source,shell]
----
C:\Temp> set PATH=%PATH%;c:\my_project
C:\Temp> medley
{1 {:id 1}}
----
